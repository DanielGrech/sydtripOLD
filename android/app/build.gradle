buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:' + androidGradlePluginVersion
    }
}

apply plugin: 'com.android.application'

repositories {
    mavenCentral()
}

def buildTime() {
    def df = new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

def whoami() {
    def whoami = 'whoami'.execute().text
    return whoami == null ? '' : whoami.trim();
}

def appVersionName = "${appVersionMajor}.${appVersionMinor}.${appVersionPatch}"
def appVersionCode = appVersionMajor * 10000 + appVersionMinor * 1000 + appVersionPatch * 100

def appPackageName = "com.sydtrip.android.sydtrip"

android {
    compileSdkVersion androidCompileSdkVersion
    buildToolsVersion androidBuildToolsVersion

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    defaultConfig {
        minSdkVersion androidMinSdkVersion
        targetSdkVersion androidTargetSdkVersion
        applicationId appPackageName
        versionCode appVersionCode
        versionName appVersionName

        buildConfigField "boolean", "IS_RELEASE", "BUILD_TYPE.equals(\"release\")"
        buildConfigField "String", "CONTENT_AUTHORITY", "\"${appPackageName}.\" + BUILD_TYPE"
        buildConfigField "int", "DEFAULT_JOB_RETRY_LIMIT", "${defaultJobRetryLimit}"
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
        buildConfigField "String", "BUILDER", "\"${whoami()}\""
        buildConfigField "String", "ACCOUNT_TYPE", "\"com.sydtrip\""
        buildConfigField "String", "TRIP_CONTENT_PROVIDER_AUTHORITY", "\"com.sydtrip.android.sydtrip_trips\""
        buildConfigField "String", "SYNC_CONTENT_PROVIDER_AUTHORITY", "\"com.sydtrip.android.sydtrip.sync_provider\""
        buildConfigField "long", "SYNC_POLL_FREQUENCY", "${syncPollFrequencyInSeconds}"
        buildConfigField "long", "SYNC_POLL_FLEX", "${syncPollFlexInSeconds}"
        buildConfigField "long", "SYNC_MIN_PERIOD_BETWEEN", "${syncMinSecondsBetween}"
        buildConfigField "String", "SYNC_BASE_URL", "\"${syncBaseUrl}\""
        buildConfigField "String", "SYNC_FILE_MANIFEST", "\"${syncManifestPath}\""


        resValue "string", "app_name", "SydTrip"
        resValue "string", "account_type", "com.sydtrip"
        resValue "string", "content_provider_authority", "com.sydtrip.android.sydtrip"
        resValue "string", "trip_content_provider_authority", "com.sydtrip.android.sydtrip_trips"
        resValue "string", "sync_content_provider_authority", "com.sydtrip.android.sydtrip.sync_provider"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'

            buildConfigField "String", "SYNC_CONTENT_PROVIDER_AUTHORITY", "\"com.sydtrip.android.sydtrip.debug.sync_provider\""
            buildConfigField "String", "TRIP_CONTENT_PROVIDER_AUTHORITY", "\"com.sydtrip.android.sydtrip.debug._trips\""
            buildConfigField "String", "ACCOUNT_TYPE", "\"com.sydtrip.debug\""

            resValue "string", "app_name", "SydTripDebug"
            resValue "string", "account_type", "com.sydtrip.debug"
            resValue "string", "content_provider_authority", "com.sydtrip.android.sydtrip.debug"
            resValue "string", "trip_content_provider_authority", "com.sydtrip.android.sydtrip.debug._trips"
            resValue "string", "sync_content_provider_authority", "com.sydtrip.android.sydtrip.debug.sync_provider"
        }
    }

}

dependencies {
    compile 'com.android.support:support-v13:20.0.0'
    compile 'com.google.android.gms:play-services:5.+'
    compile 'com.jakewharton.timber:timber:2.4.1'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'com.squareup.dagger:dagger:1.2.2'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.jakewharton:butterknife:5.1.2'
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.path:android-priority-jobqueue:1.1.+'
    compile 'com.koushikdutta.ion:ion:1.3.8'
    compile 'org.tukaani:xz:1.5'
    compile fileTree(dir: '../libs', include: '*.jar')

    androidTestCompile 'org.easytesting:fest-assert-core:2.0M10'
}